from snakemake.utils import min_version
from pandas import read_csv, MultiIndex
from os.path import join, isfile

__author__ = "Graeme Ford"
__credits__ = [
    "Graeme Ford",
    "Prof. Michael S. Pepper",
    "Prof. Fourie Joubert",
    "Antionette Colic",
    "Fatima Barmania",
    "Sarah Turner",
    "Megan Holborn",
]
__version__ = "1.0.0"
__maintainer__ = "Graeme Ford"
__email__ = "graeme.ford@tuks.co.za"
__status__ = "Development"

# ENFORCE SNAKEMAKE VERSION CHECK:
min_version("7")


# DEFINE CONFIG FILE FOR SNAKEMAKE:
configfile: join("config", "config.json")


# SET REPORT TEMPLATE
report: "report/template.rst"


# IMPORT DATASET METADATA:
datasets = read_csv(join("input", "datasets.csv"), header=0)

# LD_LIBRARY_PATH is required to inform Python where OpenSSL library files are located. This is included otherwise none of the rules queued on the system will have this variable set.
envvars:
    "LD_LIBRARY_PATH"

# IMPORT COMMON FUNCTIONS:
include: "rules/common.py"

# SET GLOBAL DOCKER RECIPIE TO USE:
container: "docker://graemeford/pipeline-os"

# group: VALIDATE
rule wipeInfo:
    """
    Strip out INFO tags
    """
    log: "_logs/PREP/{dataset_name}/strip_INFO_tags.log"
    benchmark: "_benchmarks/VALIDATE/strip_INFO_tags/{dataset_name}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    input:
        vcf=lambda wildcards: datasets.loc[
            datasets["dataset_name"] == wildcards.dataset_name, "file"
        ].unique().item(),
    output:
        vcf="results/PREP/{dataset_name}/strip_INFO_tags.vcf.gz"
    shell:
        """
        bcftools annotate -x INFO,FORMAT -Oz -o {output.vcf} {input.vcf} 2>{log}
        """

# group: VALIDATE
rule normalize:
    """
    Normalise variants - split multiallelic records, left align variants, and ensure variant parsimony
    """
    log: "_logs/PREP/{dataset_name}/normalize.log"
    benchmark: "results/_benchmarks/VALIDATE/normalize/{dataset_name}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    input:
        vcf="results/PREP/{dataset_name}/strip_INFO_tags.vcf.gz",
        tbi="results/PREP/{dataset_name}/strip_INFO_tags.vcf.gz.tbi",
    output:
        vcf="results/PREP/{dataset_name}/normalize.vcf.gz"
        # pipe("results/PREP/{dataset_name}/normalize.vcf.gz")
    shell:
        """
        bcftools norm -m -any -O z -o {output.vcf} < {input.vcf} 2>{log}
        """

# group: VALIDATE
rule sort:
    """
    Ensure consistency of chromosome nomenclature for each dataset and sort by chromosome
    """
    log: "_logs/PREP/{dataset_name}/sort.log"
    benchmark: "_benchmarks/VALIDATE/sort/{dataset_name}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    params:
        memory=search("memory", "VALIDATE"),
    input:
        vcf="results/PREP/{dataset_name}/normalize.vcf.gz",
        tbi="results/PREP/{dataset_name}/normalize.vcf.gz.tbi"
    output:
        vcf="results/PREP/{dataset_name}/sort.vcf.gz"
    shell:
        # TODO: Break up this internal piping and use modular rules. Especially with sort operations which have different memory requirements, worthy of consideration in a data streaming environment.
        """
        bcftools sort -m {params.memory} -T results/PREP/{wildcards.dataset_name} -O z -o {output.vcf} < {input.vcf} 2>{log}
        """

# group: VALIDATE
rule filter:
    """
    Remove variant types we cant yet analyse
    """
    log: "_logs/PREP/{dataset_name}/filter.log"
    benchmark: "_benchmarks/VALIDATE/filter/{dataset_name}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    params:
        memory=search("memory", "VALIDATE")
    input:
        vcf="results/PREP/{dataset_name}/sort.vcf.gz",
        tbi="results/PREP/{dataset_name}/sort.vcf.gz.tbi"
    output:
        vcf="results/PREP/{dataset_name}/filter.vcf.gz"
        # pipe("results/PREP/{dataset_name}/filter.vcf.gz")
    shell:
        """
        bcftools view -v snps,indels -f PASS -O z -o {output.vcf} < {input.vcf} 2>{log}
        """

# group: VALIDATE
rule annotate:
    """
    This rule uses a VCF file provided by RefSeq to annotate known variants
    """
    log: "_logs/PREP/{dataset_name}/annotate.log"
    benchmark: "_benchmarks/VALIDATE/annotate/{dataset_name}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    input: 
        vcf="results/PREP/{dataset_name}/filter.vcf.gz",
        tbi="results/PREP/{dataset_name}/filter.vcf.gz.tbi",
        annotations="resources/annotations.vcf.gz"
    output:
        vcf="results/PREP/{dataset_name}/annotate.vcf.gz"
    shell:
        # --rename-chrs input/rename_chr.txt would go here
        """
        bcftools annotate -c ID -a {input.annotations} -O z -o {output.vcf} {input.vcf}
        """

# group: LIFTOVER
rule liftover:
    """
    This rule helps liftover data from one reference genome to another.
    """
    log: "_logs/PREP/{dataset_name}/liftover.log"
    benchmark: "_benchmarks/PREP/{dataset_name}/liftover.benchmark"
    envmodules:
        config["environment"]["envmodules"]["piccard"]
    params:
        chainFile=join("resources", "hg19ToHg38.over.chain"),
        ref=join(
            *next(
                reference_genome["file_path"]
                for reference_genome in config["reference-genomes"]
                if reference_genome["version"] == "GRCh38"
            ),
        ),
    input:
        vcf="results/PREP/{dataset_name}/annotate.vcf.gz",
        tbi="results/PREP/{dataset_name}/annotate.vcf.gz.tbi",
    output:
        vcf="results/PREP/{dataset_name}/liftover.vcf.gz",
        rejected="results/PREP/{dataset_name}/liftover_rejected.vcf.gz"
    shell:
        """
        PicardCommandLine LiftoverVcf I={input.vcf} O={output.vcf} R={params.ref} C={params.chainFile} REJECT={output.rejected} 2>{log}
        """

# group: VALIDATE
rule tabix:
    """
    Generate tabix-index.
    """
    log: "_logs/{operation}/{dataset_name}/{output}.log"
    benchmark: "_benchmarks/{operation}/{dataset_name}/{output}.benchmark"
    resources:
        cpus=search("cores", "VALIDATE"),
        nodes=search("nodes", "VALIDATE"),
        queue=search("queue", "VALIDATE"),
        walltime=search("walltime", "VALIDATE")
    envmodules:
        config["environment"]["envmodules"]["bcftools"],
    input:
        "results/{operation}/{dataset_name}/{output}.vcf.gz"
    output:
        "results/{operation}/{dataset_name}/{output}.vcf.gz.tbi"
    shell:
        """
        tabix -p vcf {input}
        """


# TODO: Potential gist snippet
rule stats:
    log:  "_logs/PREP/{dataset_name}/{operation}_bcfstats.log"
    benchmark:  "_benchmarks/PREP/{dataset_name}/{operation}_bcfstats.benchmark"
    envmodules:
        config["environment"]["envmodules"]["latex"],
        config["environment"]["envmodules"]["bcftools"]
    input: 
        final = "results/PREP/{dataset_name}/{operation}.vcf.gz",
        final_tbi = "results/PREP/{dataset_name}/{operation}.vcf.gz.tbi",
        initial = lambda wildcards: getattr(rules, str(wildcards.operation)).input.vcf,
        initial_tbi = lambda wildcards: getattr(rules, str(wildcards.operation)).input.tbi
    output: 
        report(
            directory("results/PREP/{dataset_name}/{operation}"),
            patterns=["{name}.png"],
            category="Data processing and validation",
            subcategory="{dataset_name}",
            caption="report/stats.rst",
            labels={
                "Dataset": "{dataset_name}",
                "Operation": "{operation}",
            }
            )
    shell:
        """
        bcftools stats {input.initial} {input.final} > All.vchk
        plot-vcfstats -p {output} All.vchk
        """

# Start conditional output file list ocnstruction:
output_files = list()

output_files.append(
    expand(
        "results/PREP/{dataset_name}/sort.vcf.gz", 
        dataset_name=list(datasets["dataset_name"])
        )
)
output_files.append(
    expand(
        "results/PREP/{dataset_name}/sort.vcf.gz.tbi", 
        dataset_name=list(datasets["dataset_name"])
        )
)

# Include output for rule 'annotate'
output_files.append(
    expand(
        "results/PREP/{dataset_name}/annotate.vcf.gz", 
        dataset_name=list(datasets["dataset_name"])
        )
)
output_files.append(
    expand(
        "results/PREP/{dataset_name}/annotate.vcf.gz.tbi", 
        dataset_name=list(datasets["dataset_name"])
        )
)

# Include output for rule 'normalize'
output_files.append(
    expand(
        "results/PREP/{dataset_name}/normalize.vcf.gz", 
        dataset_name=list(datasets["dataset_name"])
        )
)
output_files.append(
    expand(
        "results/PREP/{dataset_name}/normalize.vcf.gz.tbi", 
        dataset_name=list(datasets["dataset_name"])
        )
)


# Include output for rule 'filter'
output_files.append(
    expand(
        "results/PREP/{dataset_name}/filter.vcf.gz", 
        dataset_name=list(datasets["dataset_name"])
        )
)
output_files.append(
    expand(
        "results/PREP/{dataset_name}/filter.vcf.gz.tbi", 
        dataset_name=list(datasets["dataset_name"])
        )
)

# [EACH] add qaulity-control snapshot reports
# output_files.append(
#     expand(
#         "results/PREP/{dataset_name}/{operation}/", 
#         dataset_name=list(datasets["dataset_name"]),
#         operation=["normalize", "sort", "sort", "filter", "annotate"]
#         )
# )


# Here, we use a `Pandas` `MultiIndex` to group our datasets by ["reference_genome", "dataset_name"]. This allows
# us to iterate over both the `reference_genome` columns unique values AND the `dataset_name` columns and access
# their unique values.
#
# > Think of `MultiIndexes` as an Excel Sheet with the header row and the index columns (Can have more than one)
# > are fixed. Now take that, but add a Z-dimension to the data in the middle that can stills croll around. It 
# > now has levels and you go up or down however many levels there are and you get a whole new set of values for 
# > the colunms and row indexes.
# > 
# > This way of working with data helps us select our data in an intuitive way that also filters it by nature, 
# > since to select the data, you simply need to be able to describe what data you want. This is better for code 
# > compatability as it relies less heavily on query method implementation, and more on data science principles.
#
# TODO: Evaluate porting metadata to MultiIndexes where appropriate.
for reference_genome, groupby_subset in datasets.set_index(["reference_genome", "file"]).groupby(level=0): # [FOR] all unqiue (dataset_name and reference_genome) column combinations present
    if reference_genome != "GRCh38" and groupby_subset is not None: # [IF] reference genome version
        for dataset_name in groupby_subset.index.get_level_values(0): # [FOR] the column in our MultiIndex that contains the dataset_name's in this subset
            output_files.append(
                # [EACH] add liftover request for the DAG
                f"results/PREP/{dataset_name}/liftover.vcf.gz"
            )
            output_files.append(
                # [EACH]{QC} add liftover snapshot
                f"results/PREP/{dataset_name}/liftover.pdf"
            )
rule all:
    """
    Catch-all rule to trigger auto-run of all processes. This process will be fired automatically in absence of explicit process name given by cli-argument.
    """
    default_target: True
    log:
        "logs/ALL/ALL.log",
    input:
        [item for sublist in output_files for item in sublist],